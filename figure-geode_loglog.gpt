set term x11

rs_color(z, l_emit) = l_emit*(z + 1)

set style line 2  lc rgb '#ee4562' lt 1 lw 4 # --- blue
set style line 3  lc rgb '#bb3552' lt 1 lw 4 #      .
set style line 4  lc rgb '#772542' lt 1 lw 4 #      .
set style line 5  lc rgb '#111532' lt 1 lw 4 #      .

# stupid way to do mapping names to linesytles...
name2num(z)=(z == "2" ? 5 : z== "1" ? 4 : z== "0.2" ? 3 : z== "0.05" ? 2 : 6)

# indicated color
sig1color = '#ff8000'
sig2color = '#aaaaff'
currentcolor = '#eeeeee'

unset key

# Hold the ranges fixed!
set yrange [2.8:-2]
set xrange [0:100]

# See if they finally fixed this (it is supposedly fixed...)
set style fill transparent solid 0.4 border 
# Nope.

#
# To draw the colored regions in a non-artifacted way
#  1) draw them to a table
#  2) convert the table to polypoints
#  3) render as a filled polygon
#

set table "data/aligo_gwtc1.tab"
plot '++' u 1:(1.7):(-1.5) w filledcu lc rgb "blue" not

unset table

eval(system('./skript.py data/aligo_gwtc1.tab first polypoints'))
eval(sprintf("set object 2 polygon behind clip fillcolor rgb sig1color %s", polypoints))

# Now begin making the graph
set term cairolatex color standalone size 7cm,7cm
set output "tex_scratch/figure-geode_loglog.tex"

# Enter multiplot
set multiplot
lmargins = 0.15
rmargins = 0.95

set tmargin at screen 0.21; set bmargin at screen 0.9
set lmargin at screen lmargins; set rmargin at screen rmargins

# Formatting lines
#unset xtics
set ytics offset 0.5,0
set format x ''
set format y '\scalebox{0.5}{$%.1f$}'
# Add the legend hackstyle
unset key

set label 1 '\scalebox{0.5}{Salpeter IMF}' at 72, 2.47 front
set label 2 '\scalebox{0.5}{LIGO GWTC-1 $\alpha$}' at 65, 0.55 front

plot "+" using (NaN):(NaN):(NaN) w filledcu lc rgb sig2color t '\scalebox{0.5}{LIGO previous $\alpha$ 1-$\sigma$}', "+" using (NaN):(NaN):(NaN) w filledcu lc rgb sig1color t '\scalebox{0.5}{LIGO GWTC-1 $\alpha$ 1-$\sigma$}', 0.4 lc rgb sig1color lw 5 dt 1 not, 2.35 lc rgb sig2color lw 2 dt 2 not

unset label 1
unset label 2

# because multiplot is stupid
unset key
unset xlabel
unset ylabel
unset xtics
unset ytics
unset border

# Clean up the objects so we don't draw them again
unset object 1
unset object 2
unset object 3

set ylabel '\scalebox{0.5}{Effective $\mathrm{d}n/\mathrm{d}m \equiv m^{-\alpha}$ exponent ($\mathrm{d}\left[\log \mathrm{d}n/\mathrm{d}m\right]/\mathrm{d}(\log m)$)}' offset -2.3,0

zs = "0.2 0.05"
set label 1 '\scalebox{0.5}{$z = 0.05$}' at 26, 1.0 rotate by -20 front
set label 2 '\scalebox{0.5}{$z = 0.2$}' at 17, 1.3 rotate by -23 front

plot for [z in zs] sprintf("data/%s.dat", z) every :::3::3 using (10**$1):(-$2) w l t sprintf('\scalebox{0.5}{$z = %s$}', z) ls name2num(z)
unset label 1
unset label 2
unset ylabel

set border
set tmargin at screen 0.2; set bmargin at screen 0.1
set yrange [0:1]
set format x '\scalebox{0.5}{$%.0f$}'
unset ylabel

# Adjust the background of the indicated sample
set style function filledcurves y1=0
plot 1 fs solid 0.4 lc rgb currentcolor
unset object 1
unset border
set xtics nomirror offset 0,0.5
set xtics scale 0.5
set xlabel '\scalebox{0.5}{Source frame apparent BH mass ($M_\odot$)}' offset 0,1.2

# Just use rand
now=system("date +'%s'")
print rand(now)
plot "data/aligo_mergers.dat" using 2:(0.05 + rand(0)*0.9):($2+$4):($2+$3) w xerr ls 1 lc rgb '#000000' ps 0.25 pt 5 not 
unset xtics
unset xlabel
set label 1 '\scalebox{0.5}{GWTC-1 $N=22$}' at 72, 0.75 front
plot "data/aligo_mergers.dat" using 5:(0.05 + rand(0)*0.9):($5+$7):($5+$6) w xerr ls 1 lc rgb '#000000' ps 0.25 pt 5 not 

unset multiplot

set term x11
